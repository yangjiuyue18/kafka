plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'ru.spbu.apmath.pt'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.21"

    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.5.0'
    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: '3.5.0'

    implementation group: 'org.json', name: 'json', version: '20210307'

    implementation group: 'log4j', name: 'log4j', version: '1.2.17'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    implementation group: 'org.apache.spark', name: 'spark-streaming_2.12', version: '3.4.1'
    implementation group: 'org.apache.spark', name: 'spark-core_2.12', version: '3.4.1'
    implementation group: 'org.apache.spark', name: 'spark-streaming-kafka-0-10_2.12', version: '3.4.1'
}

configurations.compileClasspath {
    resolutionStrategy {
        force("org.scala-lang:scala-library:2.12.10")
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
    archiveBaseName.set('OpenWeather')
    archiveVersion.set('1.0-SNAPSHOT')
    archiveClassifier.set('all')

    // 启用 zip64 支持，防止超过 65535 条目时报错
    zip64 = true

    // 指定主类
    manifest {
        attributes(
                'Main-Class': 'ru.spbu.apmath.pt.WeatherKafkaProducerKt'
        )
    }
}

tasks.build {
    dependsOn tasks.shadowJar
}
